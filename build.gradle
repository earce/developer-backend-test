plugins {
    id 'java'
    id 'jacoco'
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task coverageReport {
    doLast {

        def slurper = new XmlSlurper()

        slurper.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
        slurper.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false)

        def missed = 0.0
        def covered = 0.0

        def xml = slurper.parse(projectDir.toString() + "/build/reports/jacoco/test/jacocoTestReport.xml")
        def counter = xml.counter.find {
            node -> node.@type == 'INSTRUCTION'
        }
        missed += counter.@missed.toDouble()
        covered += counter.@covered.toDouble()
        def total = missed + covered
        def percentage = covered / total * 100
        printf "Missed %.0f instructions%n", missed
        printf "Covered %.0f instructions%n", covered
        printf "Total %.2f%%%n", percentage
    }
}

ext {
    vertx = '4.0.2'
    jackson = '2.12.2'
    mockito = '3.8.0'
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jackson"
    testCompile group: 'org.mockito', name: 'mockito-core', version: "$mockito"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

